plugins {
    id 'maven-publish'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(15)
        }
    }

    compileJava {
        inputs.property("moduleName", projects.name)
        doFirst {
            options.compilerArgs = [
                    '--module-path', classpath.asPath,
            ]
            classpath = files()
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://maven.fabricmc.net/' }
    }

    dependencies {
        implementation 'com.google.guava:guava:29.0-jre'
        implementation 'com.google.code.gson:gson:2.8.6'
        compileOnly 'org.jetbrains:annotations:20.1.0'

        testImplementation 'junit:junit:4.13.1'
        testImplementation 'org.hamcrest:hamcrest-all:1.3'
    }

    group = 'cuchaz'
    version = '0.21.4'

    def buildNumber = System.getenv("BUILD_NUMBER")
    version = version + "+" + (buildNumber ? "build.$buildNumber" : "local")

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    java {
        withSourcesJar()
    }

    publishing {
        publications {
            "$project.name"(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                from components.java
            }
        }
    }
}

allprojects {
    publishing {
        repositories {
            mavenLocal()

            if (project.hasProperty('mavenPass')) {
                maven {
                    url = "http://mavenupload.modmuss50.me/"
                    credentials {
                        username = "buildslave"
                        password = project.getProperty('mavenPass')
                    }
                }
            }
        }
    }
}
